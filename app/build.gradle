plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.akaita.android.easylauncher'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion rootProject.ext.buildConfig.compileSdkVersion

    defaultConfig {
        versionCode 1
        versionName '1.0.0'

        applicationId "com.tailoredapps.androidapptemplate"
        setProperty("archivesBaseName", "AndroidAppTemplate-v$versionName-b$versionCode")

        targetSdkVersion rootProject.ext.buildConfig.targetSdkVersion
        minSdkVersion rootProject.ext.buildConfig.minSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'mykeyalias'
            keyPassword 'mykeypassword'
            storeFile file('../keystore.jks')
            storePassword 'mykeystorepassword'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-DEBUG'
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "backend"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "AndroidAppTemplate Dev"
            resValue "string", "leak_canary_display_activity_label", "AndroidAppTemplate Leaks"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/rest/v2/\""
            dimension "backend"
        }
        prod {
            resValue "string", "app_name", "AndroidAppTemplate"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/rest/v2/\""
            dimension "backend"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
    }
}

androidExtensions {
    experimental = true
}

apply from: rootProject.file("test-dependencies.gradle")
apply from: rootProject.file("test-dependencies-android.gradle")

dependencies {
    implementation project(path: ':core')
    implementation project(path: ':base-ui')

    implementation Libs.lifecycle_extensions
    kapt Libs.lifecycle_compiler

    implementation Libs.glide
    kapt Libs.com_github_bumptech_glide_compiler
}


easylauncher {
    iconNames "@mipmap/ic_launcher_foreground"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    defaultFlavorNaming = true

    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#47BAF2", "#ffffff")
        }
        release {}
    }
}
